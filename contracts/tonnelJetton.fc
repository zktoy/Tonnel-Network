#include "imports/merkleTreeJetton.fc";
#include "jetton/imports/jetton-utils.fc";
#include "jetton/imports/op-codes.fc";

const SEND_MODE_CARRY_ALL_REMAINING_MESSAGE_VALUE = 64;

() send_continue(int const::tx_fee_continue) impure inline {
    var msg = begin_cell()
    .store_uint(0x10, 6)
    .store_slice(my_address())
    .store_grams(const::tx_fee_continue)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_uint(0, 32)
    .store_uint(0, 64)
    .end_cell();
    send_raw_message(msg, 1);

}

() send_jetton(slice recepient, int amount, slice JETTON_MASTER_ADDRESS, cell JETTON_BYTE_CODE) impure inline {
    if (amount == 0) {
        return ();
    }
    slice jetton_wallet = calculate_user_jetton_wallet_address(my_address(), JETTON_MASTER_ADDRESS, JETTON_BYTE_CODE);

    var msg = begin_cell()
            .store_uint(0x18, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000
            .store_slice(jetton_wallet)
            .store_coins(const::tx_fee_send_jetton)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::transfer(), 32)
            .store_uint(0, 64)
            .store_coins(amount)
            .store_slice(recepient)
            .store_uint(0, 2)
            .store_uint(0, 1)
            .store_coins(0)
            .store_uint(0, 1);
    send_raw_message(msg.end_cell(), 1); ;; paying fees, revert on errors

}


() mint_tonnel(slice recepient, int amount, slice JETTON_MASTER_ADDRESS) impure inline {

    var msg = begin_cell()
            .store_uint(0x18, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000
            .store_slice(JETTON_MASTER_ADDRESS)
            .store_coins(const::tx_fee_send_jetton)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::mint(), 32)
            .store_uint(0, 64)
            .store_slice(recepient)
            .store_coins(20000000)
            .store_ref(
                    begin_cell().store_uint(op::internal_transfer(), 32)
                            .store_uint(0, 64)
                            .store_coins(amount)
                            .store_slice(JETTON_MASTER_ADDRESS)
                            .store_uint(0, 200)
                            .store_uint(0, 67)
                            .store_coins(0)
                            .store_uint(0, 1)
                            .end_cell()
            );

    send_raw_message(msg.end_cell(), 2); ;; paying fees, don't revert on errors

}

() send_ton(slice recepient, int amount, int mode) impure inline {

    var msg = begin_cell()
            .store_uint(0x10, 6)
            .store_slice(recepient)
            .store_grams(amount)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .end_cell();
    send_raw_message(msg, mode);

}

() emit_log_simple (int event_id, slice data) impure inline {
    var msg = begin_cell()
            .store_uint (12, 4) ;; ext_out_msg_info$11 src:MsgAddressInt ()
            .store_uint (1, 2)
            .store_uint (256, 9)
            .store_uint(event_id, 256)
            .store_uint(0, 64 + 32 + 2) ;; created_lt, created_at, init:Maybe, body:Either
            .store_slice(data)
            .end_cell();
    send_raw_message(msg, 0);
}


;; recv_internal is the main function of the contract and is called when it receives a message from other contracts
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore all empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    (int init, cell merkle, cell fee_info, cell nullifiers, cell jetton_info) = load_data(); ;; here we populate the storage variables

    if (init < 20) { ;; init merkle tree
        (init, merkle) = construct_tree(init, merkle);
        save_data(init, merkle, fee_info, nullifiers, jetton_info);
        return ();
    }
    int op = in_msg_body~load_uint(32); ;; by convention, the first 32 bits of incoming message is the op
    int query_id = in_msg_body~load_uint(64); ;; also by convention, the next 64 bits contain the "query id", although this is not always the case
    if(init == 21) { ;; continue merkle tree operation
        if(op == op::transfer_notification()) {
            int send_amount = in_msg_body~load_coins(); ;; Jettons send

            slice jetton_info_slice = jetton_info.begin_parse();
            slice jetton_master_address = jetton_info_slice~load_msg_addr();
            cell JETTON_BYTECODE = jetton_info_slice~load_ref();
            slice origin_sender = in_msg_body~load_msg_addr(); ;; transfer origin address (not used)
            throw_unless(error::jetton_sender, equal_slices(sender_address, calculate_user_jetton_wallet_address(my_address(), jetton_master_address, JETTON_BYTECODE)));
            throw_unless(error::fund_fee, msg_value > const::tx_fee_send_jetton);
            send_jetton(origin_sender, send_amount, jetton_master_address, JETTON_BYTECODE);
            send_ton(origin_sender, msg_value - const::tx_fee_send_jetton - 15000000, 1);

            return ();
        }
        if(op != op::withdraw) {
            throw_unless(error::access_denied, op == 0);
            (int index, init, merkle) = insert(0, 0, merkle);
            save_data(init, merkle, fee_info, nullifiers, jetton_info);
            return ();
        }

    }

    slice fee_info_slice = fee_info.begin_parse();
    slice fee_recepient = fee_info_slice~load_msg_addr();
    int fee_percent = fee_info_slice~load_uint(10);
    slice tonnel_jetton_master_address = fee_info_slice~load_msg_addr();
    int tonnel_mint_amount_deposit = fee_info_slice~load_uint(32); ;; amount of jetton to mint for depositor, it should be multiplied by 10^9
    int tonnel_mint_amount_relayer = fee_info_slice~load_uint(32); ;; amount of jetton to mint for relayers, it should be multiplied by 10^9
    int const::tx_fee = fee_info_slice~load_coins();
    int const::tx_fee_continue = fee_info_slice~load_coins();
    if (op == op::transfer_notification()) {
        ;; should check if jetton is valid
        int send_amount = in_msg_body~load_coins(); ;; Jettons send

        slice jetton_info_slice = jetton_info.begin_parse();
        slice jetton_master_address = jetton_info_slice~load_msg_addr();
        cell JETTON_BYTECODE = jetton_info_slice~load_ref();
        int fee_amount = POOL_SIZE * fee_percent / 1000; ;; 2% fee
        throw_unless(error::jetton_sender, equal_slices(sender_address, calculate_user_jetton_wallet_address(my_address(), jetton_master_address, JETTON_BYTECODE)));
        slice origin_sender = in_msg_body~load_msg_addr(); ;; transfer origin address

        if(msg_value < const::tx_fee + const::tx_fee_send_jetton) {
            send_jetton(origin_sender, send_amount, jetton_master_address, JETTON_BYTECODE);
            return ();
        }
        if (send_amount != POOL_SIZE + fee_amount) {
            send_jetton(origin_sender, send_amount, jetton_master_address, JETTON_BYTECODE);
            return ();
        }

        cell commitment = in_msg_body~load_ref();

        slice commitment_slice = commitment.begin_parse();

        int commitment_value = commitment_slice~load_uint(256);

        send_jetton(fee_recepient, fee_amount, jetton_master_address, JETTON_BYTECODE);
        mint_tonnel(origin_sender, tonnel_mint_amount_deposit * 1000000000, tonnel_jetton_master_address);


        (int index, init, merkle) = insert(commitment_value, 1, merkle);

        emit_log_simple(op::deposit, begin_cell().store_uint(commitment_value, 256).store_uint(index, 32).end_cell().begin_parse());
        save_data(init, merkle, fee_info, nullifiers, jetton_info);
        send_continue(const::tx_fee_continue);

        return ();
    }
    if (op == op::withdraw) {
        ;;
        ;;
        ;; Proof inputs
        ;;
        ;;
        cell withdraw_info = in_msg_body~load_ref();
        slice withdraw_info_slice1 = withdraw_info.begin_parse();
        int root = withdraw_info_slice1~load_uint(256);
        int nullifier_hash = withdraw_info_slice1~load_uint(256);
        throw_unless(error::verify_failed_double_spend, is_unknown_nullifier(nullifier_hash, nullifiers));

        int fee_coef = withdraw_info_slice1~load_uint(10);
        cell addresses = withdraw_info_slice1~load_ref();
        slice addresses_slice = addresses.begin_parse();
        slice recipient = addresses_slice~load_msg_addr();
        throw_unless(error::verify_failed_input, root < FIELD_SIZE);
        throw_unless(error::verify_failed_input, nullifier_hash < FIELD_SIZE);
        throw_unless(error::verify_failed_fee, fee_coef < 1000);
        throw_unless(error::verify_failed_root, is_known_root(root));

        ;;
        ;;
        ;; Verifying Key constants
        ;;
        ;;


        slice alf1 = begin_cell().store_uint(189113849972531, 48).store_uint(214437465763263, 48).store_uint(263914761327948, 48).store_uint(237576811802638, 48).store_uint(30878666001041, 48).store_uint(142651362995775, 48).store_uint(77007716138746, 48).store_uint(119932691197078, 48).end_cell().begin_parse();
        slice IC0 = begin_cell().store_uint(185234007627208, 48).store_uint(244640744560658, 48).store_uint(144442789427744, 48).store_uint(145966279664713, 48).store_uint(174656771410752, 48).store_uint(10182432446067, 48).store_uint(201625548889433, 48).store_uint(247130673232669, 48).end_cell().begin_parse();
        slice IC1 = begin_cell().store_uint(147261977236668, 48).store_uint(243840351087340, 48).store_uint(125958757807818, 48).store_uint(133799041747222, 48).store_uint(230369696623980, 48).store_uint(124661718977352, 48).store_uint(85880269694014, 48).store_uint(85984802413192, 48).end_cell().begin_parse();
        slice IC2 = begin_cell().store_uint(187939457446490, 48).store_uint(274518153428821, 48).store_uint(278200914879624, 48).store_uint(223491615525785, 48).store_uint(123121679704913, 48).store_uint(196813090447154, 48).store_uint(17827089085261, 48).store_uint(150256821614282, 48).end_cell().begin_parse();
        slice IC3 = begin_cell().store_uint(178304246123911, 48).store_uint(199715351017763, 48).store_uint(101053488917539, 48).store_uint(68439659645170, 48).store_uint(226333961587762, 48).store_uint(51988622319415, 48).store_uint(204665741336983, 48).store_uint(236254217010421, 48).end_cell().begin_parse();
        slice IC4 = begin_cell().store_uint(162434706651903, 48).store_uint(264743452514069, 48).store_uint(112412402515081, 48).store_uint(82885480618351, 48).store_uint(25917951735505, 48).store_uint(83754027062271, 48).store_uint(52245709302839, 48).store_uint(259676451111832, 48).end_cell().begin_parse();
        slice beta2 = begin_cell().store_uint(53983131665637676933791645538, 96).store_uint(9270504936710799060868431770, 96).store_uint(69352094514504141525714669547, 96).store_uint(55771656108036236707980343141, 96).store_uint(180935328918272288278469744, 96).store_uint(23135862158652605498526059376, 96).store_uint(42398820399778041347200073775, 96).store_uint(37413453413723433454959255301, 96).end_cell().begin_parse();
        slice gamma2 = begin_cell().store_uint(45765300665918301128790692768, 96).store_uint(42137484379184671317244818970, 96).store_uint(56280794141317933024253112594, 96).store_uint(6059577009407902906031811454, 96).store_uint(709198854518927808499549479, 96).store_uint(14165060894806320894179293954, 96).store_uint(55805278558791767872231965478, 96).store_uint(52000413785700509085167893944, 96).end_cell().begin_parse();
        slice delta2 = begin_cell().store_uint(55759467878813050231413985239, 96).store_uint(61270536824888820576878155419, 96).store_uint(56634014838157022710170541747, 96).store_uint(36573918027882474462137342810, 96).store_uint(2003486576362317885654146295, 96).store_uint(30292277287856656074141497835, 96).store_uint(10563845723753665835120835881, 96).store_uint(5390941652862167086734042896, 96).end_cell().begin_parse();



        cell A_cell = withdraw_info_slice1~load_ref();
        cell B_cell = withdraw_info_slice1~load_ref();
        cell C_cell = withdraw_info_slice1~load_ref();
        slice A = A_cell.begin_parse();
        slice B = B_cell.begin_parse();
        slice C = C_cell.begin_parse();
        slice vk_x = IC0;


        vk_x = bls_g1_add(vk_x, bls_g1_mul(IC1, root));
        vk_x = bls_g1_add(vk_x, bls_g1_mul(IC2, nullifier_hash));
        vk_x = bls_g1_add(vk_x, bls_g1_mul(IC3, recipient.preload_uint(256)));
        vk_x = bls_g1_add(vk_x, bls_g1_mul(IC4, fee_coef));

        slice neg_a = bls_g1_negate(A);
        int success = bls_pairing_check(neg_a,
                B,
                alf1,
                beta2,
                vk_x,
                gamma2,
                C,
                delta2,
                4);
        ifnot (success) {
            throw(error::verify_failed);
        }

        nullifiers~udict_set(256, nullifier_hash, begin_cell().end_cell().begin_parse());
        int fee_amount = fee_coef * POOL_SIZE / 1000;

        slice jetton_info_slice = jetton_info.begin_parse();
        slice jetton_master_address = jetton_info_slice~load_msg_addr();
        cell JETTON_BYTECODE = jetton_info_slice~load_ref();

        send_jetton(recipient, POOL_SIZE - fee_amount, jetton_master_address, JETTON_BYTECODE);
        send_jetton(sender_address, fee_amount, jetton_master_address, JETTON_BYTECODE);
        mint_tonnel(sender_address, tonnel_mint_amount_relayer * 1000000000, tonnel_jetton_master_address);

        save_data(init, merkle, fee_info, nullifiers, jetton_info);

        send_ton(sender_address, 0, SEND_MODE_CARRY_ALL_REMAINING_MESSAGE_VALUE);

        return ();


    }
    throw_unless(401, equal_slices(sender_address, fee_recepient));
    if (op == op::set_fee) {
        cell fee_info_new = in_msg_body~load_ref();
        save_data(init, merkle, fee_info_new, nullifiers, jetton_info);
        return ();

    }
    throw(0xffff); ;; if the message contains an op that is not known to this contract, we throw
}



(int) check_verify(slice in_msg_body) method_id {
    in_msg_body~load_uint(32 + 64);
    (_, _, _, cell nullifiers, _) = load_data(); ;; here we populate the storage variables

    slice alf1 = begin_cell().store_uint(189113849972531, 48).store_uint(214437465763263, 48).store_uint(263914761327948, 48).store_uint(237576811802638, 48).store_uint(30878666001041, 48).store_uint(142651362995775, 48).store_uint(77007716138746, 48).store_uint(119932691197078, 48).end_cell().begin_parse();
    slice IC0 = begin_cell().store_uint(185234007627208, 48).store_uint(244640744560658, 48).store_uint(144442789427744, 48).store_uint(145966279664713, 48).store_uint(174656771410752, 48).store_uint(10182432446067, 48).store_uint(201625548889433, 48).store_uint(247130673232669, 48).end_cell().begin_parse();
    slice IC1 = begin_cell().store_uint(147261977236668, 48).store_uint(243840351087340, 48).store_uint(125958757807818, 48).store_uint(133799041747222, 48).store_uint(230369696623980, 48).store_uint(124661718977352, 48).store_uint(85880269694014, 48).store_uint(85984802413192, 48).end_cell().begin_parse();
    slice IC2 = begin_cell().store_uint(187939457446490, 48).store_uint(274518153428821, 48).store_uint(278200914879624, 48).store_uint(223491615525785, 48).store_uint(123121679704913, 48).store_uint(196813090447154, 48).store_uint(17827089085261, 48).store_uint(150256821614282, 48).end_cell().begin_parse();
    slice IC3 = begin_cell().store_uint(178304246123911, 48).store_uint(199715351017763, 48).store_uint(101053488917539, 48).store_uint(68439659645170, 48).store_uint(226333961587762, 48).store_uint(51988622319415, 48).store_uint(204665741336983, 48).store_uint(236254217010421, 48).end_cell().begin_parse();
    slice IC4 = begin_cell().store_uint(162434706651903, 48).store_uint(264743452514069, 48).store_uint(112412402515081, 48).store_uint(82885480618351, 48).store_uint(25917951735505, 48).store_uint(83754027062271, 48).store_uint(52245709302839, 48).store_uint(259676451111832, 48).end_cell().begin_parse();
    slice beta2 = begin_cell().store_uint(53983131665637676933791645538, 96).store_uint(9270504936710799060868431770, 96).store_uint(69352094514504141525714669547, 96).store_uint(55771656108036236707980343141, 96).store_uint(180935328918272288278469744, 96).store_uint(23135862158652605498526059376, 96).store_uint(42398820399778041347200073775, 96).store_uint(37413453413723433454959255301, 96).end_cell().begin_parse();
    slice gamma2 = begin_cell().store_uint(45765300665918301128790692768, 96).store_uint(42137484379184671317244818970, 96).store_uint(56280794141317933024253112594, 96).store_uint(6059577009407902906031811454, 96).store_uint(709198854518927808499549479, 96).store_uint(14165060894806320894179293954, 96).store_uint(55805278558791767872231965478, 96).store_uint(52000413785700509085167893944, 96).end_cell().begin_parse();
    slice delta2 = begin_cell().store_uint(55759467878813050231413985239, 96).store_uint(61270536824888820576878155419, 96).store_uint(56634014838157022710170541747, 96).store_uint(36573918027882474462137342810, 96).store_uint(2003486576362317885654146295, 96).store_uint(30292277287856656074141497835, 96).store_uint(10563845723753665835120835881, 96).store_uint(5390941652862167086734042896, 96).end_cell().begin_parse();

    ;;
    ;;
    ;; Proof inputs
    ;;
    ;;
    cell withdraw_info = in_msg_body~load_ref();
    slice withdraw_info_slice1 = withdraw_info.begin_parse();
    int root = withdraw_info_slice1~load_uint(256);
    int nullifier_hash = withdraw_info_slice1~load_uint(256);
    int fee_coef = withdraw_info_slice1~load_uint(10);
    cell addresses = withdraw_info_slice1~load_ref();
    slice addresses_slice = addresses.begin_parse();
    slice recipient = addresses_slice~load_msg_addr();

    cell A_cell = withdraw_info_slice1~load_ref();
    cell B_cell = withdraw_info_slice1~load_ref();
    cell C_cell = withdraw_info_slice1~load_ref();
    slice A = A_cell.begin_parse();
    slice B = B_cell.begin_parse();
    slice C = C_cell.begin_parse();
    slice vk_x = IC0;
    throw_unless(error::verify_failed_input, root < FIELD_SIZE);
    throw_unless(error::verify_failed_input, nullifier_hash < FIELD_SIZE);


    vk_x = bls_g1_add(vk_x, bls_g1_mul(IC1, root));
    vk_x = bls_g1_add(vk_x, bls_g1_mul(IC2, nullifier_hash));
    vk_x = bls_g1_add(vk_x, bls_g1_mul(IC3, recipient.preload_uint(256)));
    vk_x = bls_g1_add(vk_x, bls_g1_mul(IC4, fee_coef));
    throw_unless(error::verify_failed_fee, fee_coef < 1000);
    throw_unless(error::verify_failed_root, is_known_root(root));
    throw_unless(error::verify_failed_double_spend, is_unknown_nullifier(nullifier_hash, nullifiers));

    slice neg_a = bls_g1_negate(A);
    int success = bls_pairing_check(neg_a,
            B,
            alf1,
            beta2,
            vk_x,
            gamma2,
            C,
            delta2,
            4);
    ifnot (success) {
        return 0;
    }
    return 1;
}
